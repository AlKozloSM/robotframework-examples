*** Settings ***
Library           Collections

*** Test Cases ***
прямой вызов
    ${частное}=    Поделить    6    2
    Should Be Equal As Integers    ${частное}    3
    ${частное}=    Поделить на    2    6
    Should Be Equal As Integers    ${частное}    3

частичное применение
    ${поделить_на_2}=    Свернуть    Поделить на    2
    ${частное}=    Применить функцию    ${поделить_на_2}    6
    Should Be Equal As Integers    ${частное}    3

вызов списка слов
    [Documentation]    Пример от Саши Чернина
    ${список_слов}=    Create List    Поспамить в лог    Поспамить в лог    Поспамить в лог
    Выполнить все слова    ${список_слов}

*** Keywords ***
Поделить
    [Arguments]    ${делимое}    ${делитель}
    ${частное}=    Evaluate    ${делимое} / ${делитель}
    [Return]    ${частное}

Поделить на
    [Arguments]    ${делитель}    ${делимое}
    ${частное}=    Evaluate    ${делимое} / ${делитель}
    [Return]    ${частное}

Свернуть
    [Arguments]    ${исходная_функция}    ${аргумент}
    ${функция}=    Create List    ${исходная_функция}    ${аргумент}
    [Return]    ${функция}

Применить функцию
    [Arguments]    ${частичная_функция}    ${аргумент}
    ${функция}=    Get From List    ${частичная_функция}    0
    ${аргумент0}=    Get From List    ${частичная_функция}    1
    ${result}=    Run Keyword    ${функция}    ${аргумент0}    ${аргумент}
    [Return]    ${result}

Выполнить все слова
    [Arguments]    ${список_слов}
    @{список}=    Set Variable    ${список_слов}
    :FOR    ${слово}    IN    @{список}
    \    Run Keyword    ${слово}

Поспамить в лог
    LOG    Привет
